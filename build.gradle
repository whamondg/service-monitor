buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

plugins {
  id 'groovy'
  id "com.jfrog.bintray" version "1.3.1"
  id 'com.github.johnrengelman.shadow' version '1.2.2'
}

apply plugin: 'docker'

repositories {
    maven {
        url "https://oss.jfrog.org/artifactory/repo"
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
    compile 'io.prometheus:simpleclient:0.0.10'
    compile 'io.prometheus:simpleclient_servlet:0.0.10'
    compile 'org.eclipse.jetty:jetty-servlet:7.3.1.v20110307'
}

defaultTasks 'clean', 'shadowJar', 'bintrayUpload', 'buildDocker'

jar {
    manifest {
        attributes 'Main-Class': 'whamondg.ServiceMonitor'
    }
}

def getVersionName = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    stdout.toString().trim()
}

group = 'whamondg'
version = getVersionName()

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    filesSpec {
        from 'build/libs'
        into 'whamondg/'
    }
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/whamondg/${project.name}.git'
        version {
           name = project.version
           released  = new Date()
        }
    }
}

docker {
    baseImage "library/java:latest"
    maintainer 'Gordon Whamond "gordon.whamond@googlemail.com"'
}

task versionFile {
    new File('VERSION').text = version
}

task buildDocker(type: Docker) {
    dependsOn('versionFile')
    applicationName = project.name
    runCommand "mkdir /${project.name}"
    workingDir project.name
    addFile( "build/libs/${project.name }-${version}-all.jar", "${project.name }/${project.name }.jar")
    exposePort 5050
    defaultCommand(["java", "-jar","${project.name }/${project.name }.jar"])
}
